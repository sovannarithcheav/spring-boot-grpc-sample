import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import com.google.protobuf.gradle.*

val protobufVersion by extra("3.19.1")
val protobufPluginVersion by extra("0.8.18")
val grpcVersion by extra("1.42.1")

plugins {
	id("org.springframework.boot") version "2.3.0.RELEASE"
	id("io.spring.dependency-management") version "1.0.9.RELEASE"
	id("org.jmailen.kotlinter") version "3.2.0"
	id("maven-publish")
	id("com.google.protobuf") version "0.8.18"
	kotlin("jvm") version "1.5.0"
	kotlin("plugin.spring") version "1.5.0"
	kotlin("plugin.jpa") version "1.5.0"
}

group = "com.info"
version = "0.0.1-SNAPSHOT"

repositories {
	mavenCentral()
}

allprojects {

	apply {
		plugin("org.jetbrains.kotlin.jvm")
		plugin("org.jmailen.kotlinter")
	}
	repositories {
		mavenCentral()
		maven { url = uri("https://repo.spring.io/milestone") }
		maven { url = uri("https://repo.spring.io/snapshot") }
	}
	dependencies {
		implementation("org.jetbrains.kotlin:kotlin-reflect")
		implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
	}
	java.sourceCompatibility = JavaVersion.VERSION_11

	tasks.withType<KotlinCompile> {
		kotlinOptions {
			freeCompilerArgs = listOf("-Xjsr305=strict", "-Xjvm-default=all-compatibility")
			jvmTarget = "11"
		}
	}

	tasks.withType<Test> {
		useJUnitPlatform()
	}

	kotlinter {
		reporters = arrayOf("checkstyle", "plain")
		experimentalRules = false
		disabledRules = arrayOf("no-wildcard-imports")
	}
}

dependencies {
	implementation("org.springframework.boot:spring-boot-starter-web")
	implementation("org.jetbrains.kotlin:kotlin-reflect")
	implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8")
	implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1")

	implementation("com.google.protobuf:protobuf-java:$protobufVersion")
	implementation("net.devh:grpc-client-spring-boot-starter:2.12.0.RELEASE")
	implementation("io.grpc:grpc-stub:1.44.1")
	implementation("io.grpc:grpc-protobuf:1.44.1")
	implementation("io.grpc:grpc-kotlin-stub:1.1.0")
	compileOnly("jakarta.annotation:jakarta.annotation-api:1.3.5") // Java 9+ compatibility - Do NOT update to 2.0.0

	if (JavaVersion.current().isJava9Compatible) {
		implementation("javax.annotation:javax.annotation-api:+")
	}

	implementation("com.h2database:h2")
}

tasks.withType<KotlinCompile> {
	kotlinOptions {
		freeCompilerArgs = listOf("-Xjsr305=strict", "-Xjvm-default=all-compatibility")
		jvmTarget = "11"
	}
	dependsOn("generateProto")
}

sourceSets {
	test {
		java.srcDirs.add(File("src/test/kotlin"))
	}
}


protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:${protobufVersion}"
	}
	plugins {
		id("grpc") {
			artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
		}
		id("grpckt") {
			artifact = "io.grpc:protoc-gen-grpc-kotlin:1.1.0:jdk7@jar"
		}
	}

	generateProtoTasks {
		ofSourceSet("main").forEach {
			it.builtins {
				java {}
				kotlin {}
			}
			it.plugins {
				id("kotlin")
				id("grpc")
				id("grpckt")
			}
		}
	}
}
